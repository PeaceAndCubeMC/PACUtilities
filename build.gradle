plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

sourceCompatibility = JavaVersion.VERSION_18
targetCompatibility = JavaVersion.VERSION_18

group = project.pacutilities_group
archivesBaseName = project.pacutilities_name
version = project.pacutilities_version

repositories {
    // Paper
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    // Essentials
    maven { url "https://repo.essentialsx.net/releases/" }
}

configurations {
    include.canBeResolved = true
    compileOnly.extendsFrom(include)
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paper_version}"

    compileOnly "net.essentialsx:EssentialsX:${essentials_version}"
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    configurations = [project.configurations.include]

    archiveFileName = "${archivesBaseName}-${version}.jar"
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 18
}

processResources {
    filesMatching('plugin.yml') {
        expand(
                version: version
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/PeaceAndCubeMC/PACUtilities"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
